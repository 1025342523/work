apply plugin: 'com.android.application'
def dbflow_version = "4.0.0-beta7"

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.0'

    /*版本控制*/
    def versionPropsFile = file('version.properties')
    if (versionPropsFile.canRead()) {
        Properties versionProps = new Properties()
        versionProps.load(new FileInputStream(versionPropsFile))

        def verCode = versionProps['VERSION_CODE'].toInteger()

        def runTasks = gradle.startParameter.taskNames
        if (':app:assembleRelease' in runTasks) {
            versionProps['VERSION_CODE'] = (++verCode).toString()
            versionProps.store(versionPropsFile.newWriter(), null)
        }

        defaultConfig {
            applicationId "com.yifarj.yifadinghuobao"
            minSdkVersion 15
            targetSdkVersion 26
            versionCode verCode
            versionName "1.2." + verCode
            jackOptions {
                enabled true
            }
        }
    } else {
        throw new GradleException("Could not read version.properties!")
    }


    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('.apk')) {

                        // apk名_渠道名-版本号-版本名称-编译时间.apk
//                        def fileName = "yifa_${variant.productFlavors[0].name}-v${defaultConfig.versionName}-${defaultConfig.versionCode}-${releaseTime()}.apk"
                        // 不带渠道名
                        def fileName = "YDH_v${defaultConfig.versionName}-${defaultConfig.versionCode}-${releaseTime()}.apk"
                        output.outputFile = new File(outputFile.parent, fileName)
                    }
                }
            }
        }
    }
    productFlavors.all {
        flavor -> flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
    }

}

def releaseTime() {
    return new Date().format("yyyyMMdd", TimeZone.getTimeZone("UTC"))
}


dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:26.0.0-alpha1'
    compile 'com.android.support:cardview-v7:26.0.0-alpha1'
    compile 'com.android.support:design:26.0.0-alpha1'
    testCompile 'junit:junit:4.12'
    //DBFlow
    annotationProcessor "com.github.Raizlabs.DBFlow:dbflow-processor:${dbflow_version}"
    compile "com.github.Raizlabs.DBFlow:dbflow-core:${dbflow_version}"
    compile "com.github.Raizlabs.DBFlow:dbflow:${dbflow_version}"
    // RXJava 2 support
    compile "com.github.Raizlabs.DBFlow:dbflow-rx2:${dbflow_version}"
    // RxLifecycle
    compile 'com.trello.rxlifecycle2:rxlifecycle:2.0.1'
    compile 'com.trello.rxlifecycle2:rxlifecycle-components:2.0.1'
    //RxJava
    compile 'io.reactivex.rxjava2:rxjava:2.1.0'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    //RxBinding
    compile 'com.jakewharton.rxbinding2:rxbinding:2.0.0'
    compile 'com.jakewharton.rxbinding2:rxbinding-appcompat-v7:2.0.0'
    compile 'com.jakewharton.rxbinding2:rxbinding-recyclerview-v7:2.0.0'
    //Retrofit
    compile 'com.squareup.retrofit2:retrofit:2.3.0'
    //RxJava+Retrofit
    compile 'com.squareup.retrofit2:converter-gson:2.3.0'
    compile 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
    //Glide
    compile 'com.github.bumptech.glide:glide:3.8.0'
    //OkHttp
    compile 'com.squareup.okhttp3:okhttp:3.8.0'
    compile 'com.squareup.okhttp3:logging-interceptor:3.8.0'
    //Butter knife
    compile 'com.jakewharton:butterknife:8.5.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.5.1'
    //Util
    compile 'com.blankj:utilcode:1.5.0'
    //加入购车
    compile 'com.github.mcxtzhang:AnimShopButton:V1.2.0'
    //选择器
    compile 'com.wx.wheelview:wheelview:1.3.3'
    //流式布局
    compile 'com.zhy:flowlayout-lib:1.0.3'
    //zxing二维码扫描
    compile 'com.google.zxing:core:3.2.1'
    compile 'cn.bingoogolapple:bga-qrcodecore:1.1.0@aar'
    compile 'cn.bingoogolapple:bga-zxing:1.1.0@aar'
    //日期选择
    compile 'com.brucetoo.pickview:library:1.2.3'
    //徽章控件
    compile 'q.rorbin:badgeview:1.1.0'
    //Debug
    compile 'com.facebook.stetho:stetho:1.5.0'
    compile 'com.facebook.stetho:stetho-okhttp3:1.5.0'
    //LeakCanary
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5.1'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.1'
    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.1'
}

configurations.all {
    resolutionStrategy.force 'com.google.code.findbugs:jsr305:3.0.1'
}